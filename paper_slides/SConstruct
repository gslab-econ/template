# Preliminaries
import os
import sys
sys.dont_write_bytecode = True # Don't write .pyc files

# Test for proper prerequisites and setup
from configuration_test import configuration_test
[mode, cache_dir] = configuration_test(ARGUMENTS, gslab_python_version = '4.1.0')
import gslab_scons as gs
import gslab_scons.log as log
import yaml
import atexit

# Start log after getting mode and release version
mode = ARGUMENTS.get('mode', 'develop') 
vers = ARGUMENTS.get('version', '') 
log.start_log(mode, vers)

# Define the SCons environment
env = Environment(ENV = {'PATH': os.environ['PATH']}, 
                  IMPLICIT_COMMAND_DEPENDENCIES = 0,
                  BUILDERS = {'BuildLyx':   Builder(action = gs.build_lyx),
                              'BuildLatex': Builder(action = gs.build_latex),
                              'Tablefill':  Builder(action = gs.build_tables)})

# Only computes hash if time-stamp changed
env.Decider('MD5-timestamp') 
# Extensions to be used when scanning for source files in BuildLyx.
env.EXTENSIONS = ['.eps', '.pdf', '.lyx']
SourceFileScanner.add_scanner('.lyx', Scanner(gs.misc.lyx_scan, recursive = True))
# Load paths
env['PATHS'] = yaml.load(open('config_global.yaml', 'rU'))

# Export environment
Export('env')

# Run sub-trees
SConscript('source/tables/SConscript')
SConscript('source/paper/SConscript')
SConscript('source/talk/SConscript')
Default('./build', './release')

# Additional mode options
if mode == 'cache':
    CacheDir(cache_dir)

debrief_env = {'MAXIT' : 10,
               # Folders to look in for large versioned files
               'look_in' : 'release;source',
               # Soft limits on file sizes (w/ LFS);
               'file_MB_limit_lfs' : 2,
               'total_MB_limit_lfs' : 500,   
               # Soft limits on file sizes (w/o LFS);
               'file_MB_limit' : 0.5,
               'total_MB_limit' : 125,   
               # Check if the repo requires LFS:
               'lfs_required' : gs.misc.load_yaml_value('config_global.yaml', 'prereq_git-lfs'),
               # .gitattributes location: 
               'git_attrib_path' : '../.gitattributes'}

               
atexit.register(log.end_log)
atexit.register(gs.misc.scons_debrief, target = 'state_of_repo.log', env = debrief_env)

